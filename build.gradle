buildscript {
    ext.kotlin_version = '1.1.51'
    repositories {
        mavenCentral()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
    }
}

plugins {
    id 'net.researchgate.release' version '2.6.0'
}

release {
    failOnPublishNeeded = false
}

group = 'io.mockk'

apply plugin: 'kotlin'
apply plugin: 'maven'
apply plugin: 'signing'
apply plugin: "jacoco"

sourceCompatibility = 1.6
targetCompatibility = 1.6

repositories {
    mavenCentral()
}

task javadocJar(type: Jar) {
    classifier = 'javadoc'
    from javadoc
}

task sourcesJar(type: Jar) {
    classifier = 'sources'
    from sourceSets.main.allSource
}

build {
    afterReleaseBuild.dependsOn uploadArchives
}

artifacts {
    archives javadocJar, sourcesJar
}


dependencies {
    compile 'junit:junit:4.12'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.19'
    compile 'org.javassist:javassist:3.22.0-GA'
    compile 'net.bytebuddy:byte-buddy:1.7.7'
    testCompile 'io.kotlintest:kotlintest:2.0.7'
    compileOnly 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'ch.qos.logback:logback-classic:1.+'
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'MockK'
                packaging 'jar'
                description 'Kotlin Mocking Framework'
                url 'http://www.github.com/oleksiyp/mockk'

                scm {
                    connection 'scm:git:http://www.github.com/oleksiyp/mockk/'
                    developerConnection 'scm:git:http://github.com/oleksiyp/'
                    url 'http://www.github.com/oleksiyp/mockk/'
                }

                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id 'oleksiyp'
                        name 'Oleksii Pylypenko'
                        email 'oleksiy.pylypenko@gmail.com'
                    }
                }
            }
        }
    }
}

//jacocoTestReport {
//    reports {
//        xml.enabled true
//        xml.destination "${buildDir}/reports/jacoco/report.xml"
//        csv.enabled false
//        html.destination "${buildDir}/jacocoHtml"
//    }
//}

test {
    testLogging.showStandardStreams = true
    jvmArgs '-DprojectConfigScan=false'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}