buildscript {
    ext.kotlin_version = '1.2.20'
    repositories {
        maven {
            url  "https://dl.bintray.com/jetbrains/kotlin-native-dependencies"
        }
        mavenCentral()
        jcenter()
    }
    dependencies {
        classpath "org.jetbrains.kotlin:kotlin-gradle-plugin:$kotlin_version"
        classpath "org.jetbrains.kotlin:kotlin-native-gradle-plugin:0.6.2"
    }
}

if (project.hasProperty('JDK6_HOME')) {
    tasks.withType(JavaCompile) {
        options.bootClasspath = "$JDK6_HOME/jre/lib/rt.jar"
    }
}

subprojects {
    group = 'io.mockk'

    apply plugin: 'java'
    apply plugin: 'maven'
    apply plugin: 'signing'

    sourceCompatibility = 1.6
    targetCompatibility = 1.6

    repositories {
        mavenCentral()
    }

    task javadocJar(type: Jar) {
        classifier = 'javadoc'
        from javadoc
    }

    artifacts {
        archives javadocJar
    }

    signing {
        sign configurations.archives
    }

    signArchives.onlyIf { project.hasProperty('signing.keyId') }

    if (project.hasProperty('JDK6_HOME')) {
        tasks.withType(JavaCompile) {
            options.bootClasspath = "$JDK6_HOME/jre/lib/rt.jar"
        }
    }
}