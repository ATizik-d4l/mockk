plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'java'
}

dependencies {
    compileOnly "junit:junit:4.12"
    compileOnly 'org.testng:testng:6.8'
    compileOnly 'org.junit.jupiter:junit-jupiter-api:5.0.1'
    compileOnly 'org.junit.platform:junit-platform-launcher:1.0.1'
    compileOnly 'net.bytebuddy:byte-buddy:1.7.9'
    runtimeOnly 'net.bytebuddy:byte-buddy:1.7.9'
    compileOnly 'net.bytebuddy:byte-buddy-agent:1.7.9'
    runtimeOnly 'net.bytebuddy:byte-buddy-agent:1.7.9'
    testCompileOnly "junit:junit:4.12"
    testRuntimeOnly "junit:junit:4.12"
    compile 'org.objenesis:objenesis:2.6'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

assemble.dependsOn(shadowJar)

jar.enabled = false

artifacts {
    archives shadowJar, sourcesJar, javadocJar
}

jar.dependsOn shadowJar

task copyMockKDispatcher(type: Copy) {
    dependsOn compileJava
    println(buildDir)
    from "${buildDir}/classes/main/io/mockk/proxy"
    into "${buildDir}/classes/main/io/mockk/proxy"
    include "MockKDispatcher.class"
    rename { String fileName ->
        fileName.replace('.class', '.clazz')
    }
}
classes.dependsOn(copyMockKDispatcher)

shadowJar {
    classifier = null

    exclude 'META-INF/maven/**'
    relocate 'net.bytebuddy.', 'io.mockk.shadowed.bytebuddy.'
    relocate 'org.objenesis.', 'io.mockk.shadowed.objenesis.'
    exclude "io/mockk/proxy/MockKDispatcher.class"

    manifest {
        attributes(
                "Premain-Class": "io.mockk.agent.MockKAgent",
                "Can-Redefine-Classes": false,
                "Can-Retransform-Classes": true)
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: rootProject.properties['ossrhUsername'] ?: '', password: rootProject.properties['ossrhPassword'] ?: '')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: rootProject.properties['ossrhUsername'] ?: '', password: rootProject.properties['ossrhPassword'] ?: '')
            }

            pom.project {
                name 'MockK Java Agent'
                packaging 'jar'
                description 'MockK agent to remove finals'
                url 'http://mockk.io'

                scm {
                    connection 'scm:git:git@github.com:oleksiyp/mockk.git'
                    developerConnection 'scm:git:git@github.com:oleksiyp/mockk.git'
                    url 'http://www.github.com/oleksiyp/mockk/'
                }

                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id 'oleksiyp'
                        name 'Oleksii Pylypenko'
                        email 'oleksiy.pylypenko@gmail.com'
                    }
                }
            }
        }
    }
}
