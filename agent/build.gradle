plugins {
    id 'com.github.johnrengelman.shadow' version '2.0.1'
    id 'java'
}

dependencies {
    compileOnly "junit:junit:4.12"
    compileOnly 'org.junit.jupiter:junit-jupiter-api:5.0.1'
    compileOnly 'org.junit.platform:junit-platform-launcher:1.0.1'
    compileOnly 'org.javassist:javassist:3.22.0-GA'
    runtimeOnly 'org.javassist:javassist:3.22.0-GA'
}

sourceCompatibility = 1.6
targetCompatibility = 1.6

assemble.dependsOn(shadowJar)

artifacts {
    archives shadowJar, sourcesJar, javadocJar
}

shadowJar {
    classifier = 'agent'

    exclude 'META-INF/maven/**'
    relocate 'javassist.', 'shadowed.javassist.'

    manifest {
        attributes(
                "Premain-Class": "io.mockk.agent.MockKAgent",
                "Can-Redefine-Classes": false,
                "Can-Retransform-Classes": true)
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'MockK Java Agent'
                packaging 'jar'
                description 'MockK agent to remove finals'
                url 'http://mockk.io'

                scm {
                    connection 'scm:git:git://github.com/oleksiyp/mockk.git'
                    developerConnection 'scm:git:ssh:git@github.com/oleksiyp/mockk.git'
                    url 'http://www.github.com/oleksiyp/mockk/'
                }

                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id 'oleksiyp'
                        name 'Oleksii Pylypenko'
                        email 'oleksiy.pylypenko@gmail.com'
                    }
                }
            }
        }
    }
}
