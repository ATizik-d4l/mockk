apply plugin: 'kotlin'

dependencies {
    // agent "io.mockk:mockk-agent:$mockk_version"

    compileOnly "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    compileOnly "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    compileOnly 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.19'

    compile 'org.javassist:javassist:3.22.0-GA'

    compile 'org.objenesis:objenesis:2.6'

    compileOnly 'org.slf4j:slf4j-api:1.7.25'

    testCompile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
    testCompile "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"
    testCompile 'org.jetbrains.kotlinx:kotlinx-coroutines-core:0.19'

    testCompile 'junit:junit:4.12'

    testRuntime "org.junit.platform:junit-platform-launcher:1.0.1"
    testRuntime "org.junit.jupiter:junit-jupiter-engine:5.0.1"

    testCompile 'org.slf4j:slf4j-api:1.7.25'
    testCompile 'ch.qos.logback:logback-classic:1.2.3'

    testCompile 'io.kotlintest:kotlintest:2.0.7'

    testCompile project(":mockk-agent")
}

test {
    testLogging.showStandardStreams = true
    jvmArgs '-DprojectConfigScan=false'
}

kotlin {
    experimental {
        coroutines "enable"
    }
}

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: properties['ossrhUsername'] ?: '', password: properties['ossrhPassword'] ?: '')
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: properties['ossrhUsername'] ?: '', password: properties['ossrhPassword'] ?: '')
            }

            pom.project {
                name 'MockK'
                packaging 'jar'
                description 'mocking library for Kotlin'
                url 'http://mockk.io'

                scm {
                    connection 'scm:git:git@github.com:oleksiyp/mockk.git'
                    developerConnection 'scm:git:git@github.com:oleksiyp/mockk.git'
                    url 'http://www.github.com/oleksiyp/mockk/'
                }

                licenses {
                    license {
                        name 'Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0'
                    }
                }

                developers {
                    developer {
                        id 'oleksiyp'
                        name 'Oleksii Pylypenko'
                        email 'oleksiy.pylypenko@gmail.com'
                    }
                }
            }
        }
    }
}
